plugins {
    id 'java'
    id 'maven-publish'
    id("com.gradleup.shadow") version "8.3.0"
}

group = 'com.craftmend.openaudiomc'
version = "${__oaVersion}" // equivalent to ${oa.version} in Maven

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        name 'spigotmc-repo'
    }
    maven {
        url 'https://oss.sonatype.org/content/groups/public/'
        name 'sonatype'
    }
    maven {
        url 'https://repo.alessiodp.com/releases/'
        name 'alessiodp-repo'
    }
    maven {
        url 'https://jitpack.io'
        name 'jitpack.io'
    }
}

dependencies {
    compileOnly project(':plugin')
    compileOnly "org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT"
    compileOnly "org.projectlombok:lombok:1.18.22"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

shadowJar {
    archiveClassifier.set("")
    archiveFileName.set("vistas-client-${version}.jar")
    relocate('de.tr7zw.changeme.nbtapi', 'com.craftmend.thirdparty.nbt')
    relocate('io.', 'com.craftmend.thirdparty.io')
    relocate('org.json.', 'com.craftmend.thirdparty.json')
    relocate('reactor.', 'com.craftmend.thirdparty.reactor')
    relocate('okhttp3', 'com.craftmend.thirdparty.okhttp3')
    relocate('okio', 'com.craftmend.thirdparty.okio')
    relocate('org.projectlombok', 'com.craftmend.thirdparty.org')
    relocate('org.mapdb', 'com.craftmend.thirdparty.mapdb')
    relocate('kotlin', 'com.craftmend.thirdparty.kotlin')
}

jar {
    from sourceSets.main.allSource
    archiveBaseName.set('vistas-client')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact shadowJar
        }
    }
}
